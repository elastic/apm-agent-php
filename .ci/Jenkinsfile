#!/usr/bin/env groovy

@Library('apm@current') _

pipeline {
  agent { label 'linux && docker && ubuntu-18.04 && immutable' }
  environment {
    REPO = 'apm-agent-php'
    BASE_DIR = "src/go.elastic.co/apm/${env.REPO}"
    SLACK_CHANNEL = '#apm-agent-php'
    NOTIFY_TO = 'build-apm+apm-agent-php@elastic.co'
    ONLY_DOCS = "false"
    GITHUB_CHECK_ITS_NAME = 'Integration Tests'
    ITS_PIPELINE = 'apm-integration-tests-selector-mbp/master'
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '20', artifactNumToKeepStr: '20', daysToKeepStr: '30'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
    rateLimitBuilds(throttle: [count: 60, durationName: 'hour', userBoost: true])
    quietPeriod(10)
  }
  triggers {
    issueCommentTrigger('(?i).*jenkins\\W+run\\W+(?:the\\W+)?tests(?:\\W+please)?.*')
  }
  parameters {
    string(name: 'VERSION', defaultValue: '', description: "What's the version to be bumped when doing a release?")
  }
  stages {
    stage('Initializing'){
      options {
        skipDefaultCheckout()
        timeout(time: 1, unit: 'HOURS')
      }
      stages {
        stage('Checkout') {
          steps {
            whenTrue(isInternalCI() && isTag()) {
              notifyStatus(slackStatus: 'good', subject: "[${env.REPO}] Release tag *${env.TAG_NAME}* has been created", body: "Build: (<${env.RUN_DISPLAY_URL}|here>) for further details.")
            }
            pipelineManager([ cancelPreviousRunningBuilds: [ when: 'PR' ] ])
            deleteDir()
            gitCheckout(basedir: "${BASE_DIR}", githubNotifyFirstTimeContributor: true)
            stash allowEmpty: true, name: 'source', useDefaultExcludes: false
            dir("${BASE_DIR}"){
              // Skip all the stages except docs for PR's with asciidoc and md changes only
              whenTrue(isPR()) {
                setEnvVar('ONLY_DOCS', isGitRegionMatch(patterns: [ '.*\\.(asciidoc|md|png)' ], shouldMatchAll: true))
              }
            }
          }
        }
        stage('Prepara Release') {
          options {
            skipDefaultCheckout()
          }
          when {
            beforeAgent true
            // If not a PR and not running in the internalCI and a release with x.y format
            allOf {
              not { changeRequest() }
              not { expression { isInternalCI() } }
              expression { return (params.VERSION?.trim() && params.VERSION =~ /^\d+.\d+/) }
            }
          }
          steps {
            withGithubNotify(context: 'Prepare-Release') {
              deleteDir()
              unstash 'source'
              dir("${BASE_DIR}"){
                withGitRelease(credentialsId: '2a9602aa-ab9f-4e52-baf3-b71ca88469c7-UserAndToken') {
                  prepareRelease()
                }
              }
            }
          }
        }
        stage('BuildAndTest') {
          when {
            beforeAgent true
            expression { return env.ONLY_DOCS == "false" }
          }
          failFast false
          matrix {
            agent { label 'linux && docker && ubuntu-18.04 && immutable' }
            options { skipDefaultCheckout() }
            axes {
              axis {
                name 'PHP_VERSION'
                values '7.2', '7.3', '7.4'
              }
              axis {
                name 'DOCKERFILE'
                values 'Dockerfile', 'Dockerfile.alpine'
              }
            }
            stages {
              stage('Build') {
                steps {
                  withGithubNotify(context: "Build-${PHP_VERSION}") {
                    deleteDir()
                    unstash 'source'
                    dir("${BASE_DIR}"){
                      // When running in the CI with multiple parallel stages
                      // the access could be considered as a DDOS attack.
                      retryWithSleep(retries: 3, seconds: 5, backoff: true) {
                        sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile prepare", label: 'prepare docker image'
                      }
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile build", label: 'build'
                    }
                  }
                }
              }
              stage('PHPC Unit Tests') {
                when {
                  expression { return env.DOCKERFILE == 'Dockerfile' }
                }
                steps {
                  withGithubNotify(context: "PHPCUnit-Tests-${PHP_VERSION}", tab: 'tests') {
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile unit-test", label: 'composer'
                    }
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/unit-tests-junit.xml")
                  }
                }
              }
              stage('PHPT Tests') {
                steps {
                  withGithubNotify(context: "PHPT-Tests-${PHP_VERSION}", tab: 'tests') {
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile test", label: 'test'
                    }
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/junit.xml")
                  }
                }
              }
              stage('Generate for package') {
                steps {
                  withGithubNotify(context: "Generate-For-Package-${PHP_VERSION}") {
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile generate-for-package", label: 'generate-for-package'
                      stash includes: 'src/ext/modules/*.so', name: "generate-for-package-${PHP_VERSION}-${DOCKERFILE}"
                    }
                  }
                }
              }
              stage('PHPUnit Tests') {
                steps {
                  withGithubNotify(context: "PHPUnit-Tests-${PHP_VERSION}", tab: 'tests') {
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile composer", label: 'composer'
                    }
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/**/log_as_junit.xml")
                  }
                }
              }
            }
          }
        }
        stage('Integration Tests') {
          agent none
          when {
            beforeAgent true
            changeRequest()
          }
          steps {
            githubNotify(context: "${env.GITHUB_CHECK_ITS_NAME}", description: "${env.GITHUB_CHECK_ITS_NAME} ...", status: 'PENDING', targetUrl: "${env.JENKINS_URL}search/?q=${env.ITS_PIPELINE.replaceAll('/','+')}")
            build(job: env.ITS_PIPELINE, propagate: false, wait: false,
                  parameters: [ string(name: 'INTEGRATION_TEST', value: 'PHP'),
                                string(name: 'BUILD_OPTS', value: "--php-agent-version ${env.GIT_BASE_COMMIT}"),
                                string(name: 'GITHUB_CHECK_NAME', value: env.GITHUB_CHECK_ITS_NAME),
                                string(name: 'GITHUB_CHECK_REPO', value: env.REPO),
                                string(name: 'GITHUB_CHECK_SHA1', value: env.GIT_BASE_COMMIT) ])
          }
        }
        stage('Package Generation') {
          when {
            beforeAgent true
            expression { return env.ONLY_DOCS == "false" }
          }
          options { skipDefaultCheckout() }
          steps {
            withGithubNotify(context: "Package", tab: 'artifacts') {
              deleteDir()
              unstash 'source'
              dir("${BASE_DIR}"){
                unstash 'generate-for-package-7.2-Dockerfile'
                unstash 'generate-for-package-7.3-Dockerfile'
                unstash 'generate-for-package-7.4-Dockerfile'
                unstash 'generate-for-package-7.2-Dockerfile.alpine'
                unstash 'generate-for-package-7.3-Dockerfile.alpine'
                unstash 'generate-for-package-7.4-Dockerfile.alpine'
                sh script: "make -C packaging package", label: 'package'
                sh script: "make -C packaging info", label: 'package info'
                // checksum files are regenerated by the signing component in the internal-ci instance.
                stash(includes: 'build/packages/*', name: 'package', excludes: 'build/packages/**/*.sha512')
              }
            }
          }
          post {
            always {
              dir("${BASE_DIR}"){
                archiveArtifacts(allowEmptyArchive: true, artifacts: 'build/packages/*')
              }
            }
          }
        }
        stage('Package-Test') {
          when {
            beforeAgent true
            expression { return env.ONLY_DOCS == "false" }
          }
          failFast false
          matrix {
            agent { label 'linux && docker && ubuntu-18.04 && immutable' }
            options { skipDefaultCheckout() }
            axes {
              axis {
                name 'PHP_VERSION'
                values '7.2', '7.3', '7.4'
              }
            }
            stages {
              stage('Uninstallation testing') {
                steps {
                  withGithubNotify(context: "Package-Test-${PHP_VERSION}") {
                    deleteDir()
                    unstash 'source'
                    dir("${BASE_DIR}"){
                      unstash 'package'
                      sh script: "PHP_VERSION=${PHP_VERSION} make -C packaging uninstall", label: 'package uninstall'
                    }
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/**/log_as_junit.xml")
                  }
                }
              }
            }
          }
        }
        stage('Testing') {
          when {
            beforeAgent true
            // TODO: && false in place to disable this particular section for the time being.
            //       as agreed to avoid any misleading until this particular section has been implemented.
            expression { return env.ONLY_DOCS == "false" && false }
          }
          matrix {
            // TODO: This should be uncommented out when the implementation is in place
            // agent { label 'linux && docker && ubuntu-18.04 && immutable' }
            options { skipDefaultCheckout() }
            axes {
              axis {
                name 'PHP_VERSION'
                values '7.2', '7.3', '7.4'
              }
              axis {
                name 'FRAMEWORK'
                values 'nginx', 'apache', 'redis', 'memcached', 'mysql'
              }
            }
            stages {
              stage('Install') {
                steps {
                  // TODO: This should be uncommented out when the implementation is in place
                  // deleteDir()
                  // unstash 'source'
                  dir("${BASE_DIR}"){
                    echo 'TBD'
                  }
                }
              }
              stage('Test') {
                steps {
                  // TODO: This should be uncommented out when the implementation is in place
                  // deleteDir()
                  // unstash 'source'
                  dir("${BASE_DIR}"){
                    sh 'scripts/test-framework.sh ${PHP_VERSION} ${FRAMEWORK}'
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/junit.xml")
                  }
                }
              }
            }
            post {
              always {
                echo 'STORE docker logs'
              }
            }
          }
        }
      }
    }
    // This meta-stage happens in the internal-ci instance to be able to sign the artifacts correctly.
    stage('Release') {
      options {
        skipDefaultCheckout()
        timeout(time: 12, unit: 'HOURS')
      }
      when {
        beforeAgent true
        allOf {
          tag pattern: 'v\\d+.*', comparator: 'REGEXP'
          expression { isInternalCI() }
        }
      }
      agent { label 'linux && docker && ubuntu-18.04 && immutable' }
      environment {
        BUCKET_NAME = 'internal-ci-artifacts'
        BUCKET_SUBFOLDER = "${env.REPO}/${env.TAG_NAME}"
        BUCKET_PATH = "gs://${env.BUCKET_NAME}/${env.BUCKET_SUBFOLDER}"
        BUCKET_CREDENTIALS = 'internal-ci-gcs-plugin'
        SIGNED_ARTIFACTS = 'signed-artifacts'
        BUCKET_SUBFOLDER_SIGNED_ARTIFACTS = "${env.BUCKET_SUBFOLDER}/${env.SIGNED_ARTIFACTS}"
        BUCKET_SIGNED_ARTIFACTS_PATH = "gs://${env.BUCKET_NAME}/${env.BUCKET_SUBFOLDER_SIGNED_ARTIFACTS}"
        RELEASE_URL_MESSAGE = "(<https://github.com/elastic/apm-agent-php/releases/tag/${env.TAG_NAME}|${env.TAG_NAME}>)"
      }
      stages {
        stage('Notify') {
          options { skipDefaultCheckout() }
          steps {
            notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Release ready to be pushed",
                         body: "Please (<${env.BUILD_URL}input|approve>) it or reject within 12 hours.\n Changes: ${env.TAG_NAME}")
            setEnvVar('RELEASE', askAndWait("You are about to release version ${env.TAG_NAME}. Do you wish to release it?"))
          }
        }
        stage('Signing CI') {
          when {
            beforeAgent true
            expression { return env.RELEASE == 'true' }
          }
          options { skipDefaultCheckout() }
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}") {
              unstash 'package'
              googleStorageUpload(bucket: env.BUCKET_PATH,
                  credentialsId: env.BUCKET_CREDENTIALS,
                  pathPrefix: 'build/packages/',
                  pattern: 'build/packages/**/*',
                  sharedPublicly: false,
                  showInline: true)
              build(wait: true, propagate: true, job: 'elastic+unified-release+master+sign-artifacts-with-gpg', parameters: [string(name: 'gcs_input_path', value: "${env.BUCKET_PATH}")])
              dir("${SIGNED_ARTIFACTS}") {
                googleStorageDownload(bucketUri: "${env.BUCKET_SIGNED_ARTIFACTS_PATH}/*",
                    credentialsId: env.BUCKET_CREDENTIALS,
                    localDirectory: 'build/packages/',
                    pathPrefix: "${env.BUCKET_SUBFOLDER_SIGNED_ARTIFACTS}")
                stash allowEmpty: false, name: env.SIGNED_ARTIFACTS, useDefaultExcludes: false
              }
              archiveArtifacts(allowEmptyArchive: true, artifacts: "${SIGNED_ARTIFACTS}/**/*")
            }
          }
        }
        stage('Test-Signed-Package') {
          failFast false
          matrix {
            agent { label 'linux && docker && ubuntu-18.04 && immutable' }
            options { skipDefaultCheckout() }
            axes {
              axis {
                name 'PHP_VERSION'
                values '7.2', '7.3', '7.4'
              }
            }
            stages {
              stage('Release Test') {
                steps {
                  withGithubNotify(context: "Signed-Test-${PHP_VERSION}") {
                    deleteDir()
                    unstash 'source'
                    dir("${BASE_DIR}") {
                      unstash "${env.SIGNED_ARTIFACTS}"
                      sh script: "PHP_VERSION=${PHP_VERSION} make -C packaging install", label: 'package install'
                    }
                  }
                }
                post {
                  unsuccessful {
                    notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Release *${env.TAG_NAME}* got some test failures in the installers.", body: "Please review the signed binaries are healthy (<${env.RUN_DISPLAY_URL}|Open>)")
                  }
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/**/log_as_junit.xml")
                  }
                }
              }
            }
          }
        }
        stage('Release CI') {
          when {
            beforeAgent true
            expression { return env.RELEASE == 'true' }
          }
          options { skipDefaultCheckout() }
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}") {
              unstash "${env.SIGNED_ARTIFACTS}"
              withCredentials([string(credentialsId: '2a9602aa-ab9f-4e52-baf3-b71ca88469c7', variable: 'GITHUB_TOKEN')]) {
                sh script: 'make -f .ci/Makefile release', label: 'release'
              }
            }
          }
          post {
            success {
              notifyStatus(slackStatus: 'good', subject: "[${env.REPO}] Release *${env.TAG_NAME}* published", body: "Build: (<${env.RUN_DISPLAY_URL}|here>)\nRelease URL: ${env.RELEASE_URL_MESSAGE}")
            }
          }
        }
        stage('Test-Released-Package') {
          failFast false
          matrix {
            agent { label 'linux && docker && ubuntu-18.04 && immutable' }
            options { skipDefaultCheckout() }
            axes {
              axis {
                name 'PHP_VERSION'
                values '7.2', '7.3', '7.4'
              }
            }
            stages {
              stage('Release Test') {
                steps {
                  withGithubNotify(context: "Release-Test-${PHP_VERSION}") {
                    deleteDir()
                    unstash 'source'
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} RELEASE_VERSION=${env.TAG_NAME.replaceAll('^v', '')} make -C packaging install-release-github", label: 'package install-release-github'
                    }
                  }
                }
                post {
                  unsuccessful {
                    notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Release *${env.TAG_NAME}* published with some test failures in the installers.", body: "Please review the signed and released binaries are healthy\nBuild: (<${env.RUN_DISPLAY_URL}|here>)\nRelease URL: ${env.RELEASE_URL_MESSAGE}")
                  }
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/**/log_as_junit.xml")
                  }
                }
              }
            }
          }
        }
      }
      post {
        failure {
          notifyStatus(slackStatus: 'danger', subject: "[${env.REPO}] Release *${env.TAG_NAME}* failed", body: "Build: (<${env.RUN_DISPLAY_URL}|here>)")
        }
        always {
          script {
            currentBuild.description = "${currentBuild.description?.trim() ? currentBuild.description : ''} released"
          }
        }
      }
    }
  }
  post {
    cleanup {
      // Reporting disables in the `internal-ci` since credentials are not in place
      // OTOH it avoids duplicated notifications
      whenFalse(isInternalCI()){
        notifyBuildResult(prComment: true, analyzeFlakey: true,
                          flakyReportIdx: 'reporter-apm-agent-php-apm-agent-php-master')
      }
    }
  }
}

// TODO: create an input step to avoid this try/catch and return true/false
def askAndWait(message) {
  try {
    input(message: message, ok: 'Yes')
    return true
  } catch(err) {
    return false
  }
}

def notifyStatus(def args = [:]) {
  slackSend(channel: env.SLACK_CHANNEL, color: args.slackStatus, message: "${args.subject}. ${args.body}",
            tokenCredentialId: 'jenkins-slack-integration-token')
  // transform slack URL format '(<URL|description>)' to 'URL'.
  def bodyEmail = args.body.replaceAll('\\(<', '').replaceAll('\\|.*>\\)', '')
  emailext(subject: args.subject, to: "${env.NOTIFY_TO}", body: bodyEmail)
}

def prepareRelease() {
  def tagName = "v${params.VERSION}"
  def branchName = "prepare-release-${tagName}-${env.BUILD_NUMBER}"
  def message = "[${params.VERSION}] Bump version and create changelog"
  def warning = """> THIS PR IS AUTOMATICALLY GENERATED BY RELEASE PIPELINE."""
  def actions = """### Actions
* If changes are fine then:
  1. [Approve](${env.BUILD_URL}input) it. The pipeline will take care of the release.
  1. Close this PR since it's not required anymore.
  1. Delete branch ${branchName}.
* If changes are not correct then:
  1. [abort](${env.BUILD_URL}input) it
  1. Change and commit changes to this PR.
  1. Merge this PR.
  1. Delete branch ${branchName}.
  1. Run release [build](${env.JOB_URL}) with the VERSION parameter = ${params.VERSION}"""
  def previousVersion = sh(label: 'Get previous version', script: 'make -f .ci/Makefile previous-version', returnStdout: true).trim()
  sh label: 'Bump version', script: "VERSION=${params.VERSION} BRANCH_NAME=${env.BRANCH_NAME} make -f .ci/Makefile bump-version"
  if (sh(label: 'is version bumped?', script: 'git diff-index --quiet HEAD --', returnStatus: true) > 0) {
    // Change ci-tag pointer to be used for generating the changelog
    gitCreateTag(tag: 'ci-tag', tagArgs: '--force', pushArgs: '--force')
    // Create Changelog and verify it contains what's expected.
    sh(label: 'Create changelog', script: "GITHUB_TOKEN=${GITHUB_TOKEN} TAG_NAME=${tagName} PREVIOUS_TAG=v${previousVersion} make -f .ci/Makefile prepare-changelog changelog")
    sh(label: "Git branch ${branchName}", script: """git checkout -b ${branchName}""")
    sh(label: 'Git commit', script: """git commit -a -m "${message}" """)
    def pr = githubCreatePullRequest(title: "[RELEASE] ${params.VERSION}",
                                     description: "${warning}\n\n${actions}",
                                     labels: 'docs,release,changelog')
    notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Prepare ${params.VERSION} release steps to be validated.",
                 body: """Please (<${env.BUILD_URL}input|approve>) it or reject within 12 hours ONLY If no changes are required. Otherwise stop it and review the (<${pr}|PR>).""")
    if (askAndWait("You are about to release version ${params.VERSION}. If you approve then changes will be committed and pushed. Review ${pr}.")) {
      // Update branch.
      sh(label: "Git branch ${env.BRANCH_NAME}", script: """git checkout ${env.BRANCH_NAME}""")
      sh(label: 'Git rebase', script: """git rebase ${branchName}""")
      sh(label: 'Git push', script: """git push origin ${env.BRANCH_NAME}""")
      gitPush()
      gitCreateTag(tag: "${tagName}")
    } else {
      log(level: 'WARN', text: "Please review the PR ${pr}")
    }
  } else {
    gitCreateTag(tag: "${tagName}")
    log(level: 'INFO', text: "There are no changes to compare with. Release will happen from this commit.")
  }
}
