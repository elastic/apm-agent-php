#!/usr/bin/env groovy

@Library('apm@current') _

LOG_LEVEL_NOT_SET = 'Not set'
LOG_LEVELS = [LOG_LEVEL_NOT_SET, 'Trace', 'Debug', 'Info', 'Warning', 'Error', 'Critical', 'Off']

pipeline {
  agent { label 'ubuntu-18.04 && immutable' }
  environment {
    REPO = 'apm-agent-php'
    BASE_DIR = "src/go.elastic.co/apm/${env.REPO}"
    SLACK_CHANNEL = '#apm-agent-php'
    NOTIFY_TO = 'build-apm+apm-agent-php@elastic.co'
    ONLY_DOCS = "false"
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '20', artifactNumToKeepStr: '20', daysToKeepStr: '30'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
    rateLimitBuilds(throttle: [count: 60, durationName: 'hour', userBoost: true])
    quietPeriod(10)
  }
  triggers {
    issueCommentTrigger("${obltGitHubComments()}")
  }
  parameters {
    string(name: 'VERSION', defaultValue: '', description: "What's the version to be bumped when doing a release?")
    choice(name: 'AGENT_LOG_LEVEL', choices: LOG_LEVELS, description: "Agent's log level")
    choice(name: 'TESTS_LOG_LEVEL', choices: LOG_LEVELS, description: "Tests' log level")
    booleanParam(name: 'INCLUDE_TESTING', defaultValue: true, description: 'Should the testing stages be included?')
    booleanParam(name: 'ADD_LIBUNWIND_DEPENDENCY', defaultValue: false, description: 'Should Alpine (.apk) build have dependency on libunwind (to be able to log C call stack on crash)?')
  }
  stages {
    stage('Initializing'){
      options {
        skipDefaultCheckout()
        timeout(time: 2, unit: 'HOURS')
      }
      stages {
        stage('Checkout') {
          steps {
            whenTrue(isInternalCI() && isTag()) {
              setEnvVar('PRE_RELEASE_STAGE', 'true')
              notifyStatus(slackStatus: 'good', subject: "[${env.REPO}] Build for the release tag *${env.TAG_NAME}* has been triggered", body: "Build: (<${env.RUN_DISPLAY_URL}|here>) for further details.")
            }
            whenTrue(params.VERSION?.trim() ? true : false) {
              script {
                currentBuild.description = "${currentBuild.description?.trim() ? currentBuild.description : ''} release triggered."
              }
            }
            pipelineManager([ cancelPreviousRunningBuilds: [ when: 'PR' ] ])
            deleteDir()
            gitCheckout(basedir: "${BASE_DIR}", githubNotifyFirstTimeContributor: true)
            stash allowEmpty: true, name: 'source', useDefaultExcludes: false
            dir("${BASE_DIR}"){
              // Skip all the stages except docs for PR's with asciidoc and md changes only
              whenTrue(isPR()) {
                setEnvVar('ONLY_DOCS', isGitRegionMatch(patterns: [ '.*\\.(asciidoc|md|png)' ], shouldMatchAll: true))
              }
            }
          }
        }
        stage('Prepare for release (set version, etc.)') {
          options {
            skipDefaultCheckout()
          }
          when {
            beforeAgent true
            // If not a PR and not running in the internalCI and a release with x.y format
            allOf {
              not { changeRequest() }
              not { expression { isInternalCI() } }
              expression { return (params.VERSION?.trim() && params.VERSION =~ /^\d+.\d+/) }
            }
          }
          steps {
            initWorkspace(context: 'Prepare-Release') {
                withGitRelease(credentialsId: '2a9602aa-ab9f-4e52-baf3-b71ca88469c7-UserAndToken') {
                  prepareRelease()
                }
            }
          }
        }
        stage('Build binaries and basic testing') {
          options { skipDefaultCheckout() }
          when {
            beforeAgent true
            expression { return env.ONLY_DOCS == "false" }
          }
          failFast false
          matrix {
            agent { label 'ubuntu-18.04 && immutable' }
            axes {
              axis {
                name 'PHP_VERSION'
                // Make sure list of PHP versions supported by the Elastic APM PHP Agent is in sync.
                // See the comment in .ci/shared.sh
                values '7.2', '7.3', '7.4', '8.0', '8.1', '8.2'
              }
              axis {
                name 'DOCKERFILE'
                values 'Dockerfile', 'Dockerfile.alpine'
              }
            }
            stages {
              stage('Build PHP extension') {
                steps {
                  echo "params.INCLUDE_TESTING: ${params.INCLUDE_TESTING}"
                  echo "params.ADD_LIBUNWIND_DEPENDENCY: ${params.ADD_LIBUNWIND_DEPENDENCY}"
                  initWorkspace(context: "Build-${PHP_VERSION}") {
                    // When running in the CI with multiple parallel stages
                    // the access could be considered as a DDOS attack.
                    retryWithSleep(retries: 3, seconds: 5, backoff: true) {
                        sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} ADD_LIBUNWIND_DEPENDENCY=${params.ADD_LIBUNWIND_DEPENDENCY} make -f .ci/Makefile prepare", label: 'prepare docker image'
                    }
                    sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile build", label: 'build'
                  }
                }
              }
              stage('Static analysis and unit tests') {
                when {
                  beforeAgent true
                  expression { return params.INCLUDE_TESTING }
                }
                steps {
                  echo "params.AGENT_LOG_LEVEL: ${params.AGENT_LOG_LEVEL}"
                  echo "params.AGENT_LOG_LEVEL == null: " + (params.AGENT_LOG_LEVEL == null)
                  echo "params.TESTS_LOG_LEVEL: ${params.TESTS_LOG_LEVEL}"
                  echo "params.TESTS_LOG_LEVEL == null: " + (params.TESTS_LOG_LEVEL == null)
                  echo "addEnvVarsFromParams([]): " + addEnvVarsFromParams([])
                  withGithubNotify(context: "Static-Check-Unit-Tests-${PHP_VERSION}", tab: 'tests') {
                    withEnv(addEnvVarsFromParams([])) {
                      echo "env.ELASTIC_APM_LOG_LEVEL: ${env.ELASTIC_APM_LOG_LEVEL}"
                      echo "env.TESTS_LOG_LEVEL: ${env.TESTS_LOG_LEVEL}"
                      dir("${BASE_DIR}"){
                        sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} make -f .ci/Makefile static-check-unit-test", label: 'static-check-unit-test'
                      }
                    }
                  }
                }
                post {
                  always {
                    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/build/*junit.xml")
                  }
                }
              }
              stage('Build parts for packages') {
                steps {
                  withGithubNotify(context: "Generate-For-Package-${PHP_VERSION}") {
                    dir("${BASE_DIR}"){
                      sh script: "PHP_VERSION=${PHP_VERSION} DOCKERFILE=${DOCKERFILE} ADD_LIBUNWIND_DEPENDENCY=${params.ADD_LIBUNWIND_DEPENDENCY} make -f .ci/Makefile generate-for-package", label: 'generate-for-package'
                      stash includes: 'src/ext/modules/*.so', name: "generate-for-package-${PHP_VERSION}-${DOCKERFILE}"
                    }
                  }
                }
              }
            }
          }
        }
        stage('Build packages') {
          when {
            beforeAgent true
            expression { return env.ONLY_DOCS == "false" }
          }
          options { skipDefaultCheckout() }
          steps {
            initWorkspace(context: "Package", tab: 'artifacts') {
                // Make sure list of PHP versions supported by the Elastic APM PHP Agent is in sync.
                // See the comment in .ci/shared.sh
                packageGeneration(versions: ['7.2', '7.3', '7.4', '8.0', '8.1', '8.2'])
            }
          }
          post {
            always {
              dir("${BASE_DIR}") {
                archiveArtifacts(allowEmptyArchive: true, artifacts: 'build/packages/*')
              }
            }
          }
        }
        stage('Test packages') {
          options { skipDefaultCheckout() }
          when {
            beforeAgent true
            allOf {
              expression { return env.ONLY_DOCS == "false" }
              expression { return params.INCLUDE_TESTING }
              not { expression { isInternalCI() } }
            }
          }
          steps {
            generatePackageLifecycleTestMatrix(shouldUseSignedBinaries: false)
          }
        }
      }
    }
    // This meta-stage happens in the internal-ci instance to be able to sign the artifacts correctly.
    stage('Release (umbrella stage) (internal-ci)') {
      options {
        skipDefaultCheckout()
        timeout(time: 12, unit: 'HOURS')
      }
      when {
        beforeAgent true
        allOf {
          tag pattern: 'v\\d+.*', comparator: 'REGEXP'
          expression { isInternalCI() }
        }
      }
      agent { label 'ubuntu-18.04 && immutable' }
      environment {
        BUCKET_NAME = 'internal-ci-artifacts'
        BUCKET_SUBFOLDER = "${env.REPO}/${env.TAG_NAME}"
        BUCKET_PATH = "gs://${env.BUCKET_NAME}/${env.BUCKET_SUBFOLDER}"
        BUCKET_CREDENTIALS = 'internal-ci-gcs-plugin'
        SIGNED_ARTIFACTS = 'signed-artifacts'
        BUCKET_SUBFOLDER_SIGNED_ARTIFACTS = "${env.BUCKET_SUBFOLDER}/${env.SIGNED_ARTIFACTS}"
        BUCKET_SIGNED_ARTIFACTS_PATH = "gs://${env.BUCKET_NAME}/${env.BUCKET_SUBFOLDER_SIGNED_ARTIFACTS}"
        RELEASE_URL_MESSAGE = "(<https://github.com/elastic/apm-agent-php/releases/tag/${env.TAG_NAME}|${env.TAG_NAME}>)"
      }
      stages {
        stage('Sign packages') {
          options { skipDefaultCheckout() }
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}") {
              unstash 'package'
              googleStorageUpload(bucket: env.BUCKET_PATH,
                  credentialsId: env.BUCKET_CREDENTIALS,
                  pathPrefix: 'build/packages/',
                  pattern: 'build/packages/**/*',
                  sharedPublicly: false,
                  showInline: true)
              build(wait: true, propagate: true, job: 'elastic+unified-release+master+sign-artifacts-with-gpg', parameters: [string(name: 'gcs_input_path', value: "${env.BUCKET_PATH}")])
              dir("${SIGNED_ARTIFACTS}") {
                googleStorageDownload(bucketUri: "${env.BUCKET_SIGNED_ARTIFACTS_PATH}/*",
                    credentialsId: env.BUCKET_CREDENTIALS,
                    localDirectory: 'build/packages/',
                    pathPrefix: "${env.BUCKET_SUBFOLDER_SIGNED_ARTIFACTS}")
                stash allowEmpty: false, name: env.SIGNED_ARTIFACTS, useDefaultExcludes: false
              }
              archiveArtifacts(allowEmptyArchive: true, artifacts: "${SIGNED_ARTIFACTS}/**/*")
            }
          }
        }
        stage('Test signed packages') {
          options { skipDefaultCheckout() }
          when {
            beforeAgent true
            expression { return params.INCLUDE_TESTING }
          }
          steps {
            generatePackageLifecycleTestMatrix(shouldUseSignedBinaries: true)
          }
          post {
            unsuccessful {
              notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Release *${env.TAG_NAME}* got some test failures in the installers.", body: "Please review the signed binaries are healthy (<${env.RUN_DISPLAY_URL}|Open>)")
            }
          }
        }
        stage('Ask for approval to publish') {
          options { skipDefaultCheckout() }
          steps {
            setEnvVar('PRE_RELEASE_STAGE', 'false')
            notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}@${env.TAG_NAME}] Release ready to be published",
                         body: "Please (<${env.BUILD_URL}input|approve>) it or reject within 12 hours.\n Changes: ${env.TAG_NAME}")
            setEnvVar('HAS_APPROVAL_TO_PUBLISH_RELEASE', prompt(message: "You are about to release version ${env.TAG_NAME}. Do you wish to release it?"))
          }
        }
        stage('Publish as draft release') {
          when {
            beforeAgent true
            expression { return env.HAS_APPROVAL_TO_PUBLISH_RELEASE == 'true' }
          }
          options { skipDefaultCheckout() }
          steps {
            deleteDir()
            unstash 'source'
            dir("${BASE_DIR}") {
              unstash "${env.SIGNED_ARTIFACTS}"
              withGhEnv(version: '2.4.0') {
                sh(script: 'make -f .ci/Makefile draft-release', label: 'draft-release')
                setEnvVar('RELEASE_ID', sh(script: 'make -f .ci/Makefile get-draft-release', label: 'get-draft-release', returnStdout: true)?.trim())
                echo "RELEASE_ID: '${env.RELEASE_ID}'"
              }
            }
          }
          post {
            success {
              notifyStatus(slackStatus: 'good', subject: "[${env.REPO}] Draft Release *${env.TAG_NAME}* has been created", body: "Build: (<${env.RUN_DISPLAY_URL}|here>)\nRelease URL: ${env.RELEASE_URL_MESSAGE}")
            }
          }
        }
        stage('Verify draft release checksum') {
          when {
            beforeAgent true
            expression { return env.HAS_APPROVAL_TO_PUBLISH_RELEASE == 'true' }
          }
          options { skipDefaultCheckout() }
          steps {
            dir("${BASE_DIR}") {
              withGhEnv(version: '2.4.0') {
                sh(script: "TAG_NAME=${TAG_NAME} ORIGINAL_PACKAGES_LOCATION=build/packages make -f .ci/Makefile download-verify", label: 'download-verify')
              }
            }
          }
          post {
            always {
              archiveArtifacts(allowEmptyArchive: true, artifacts: "${BASE_DIR}/build/packages/target.sha512, ${BASE_DIR}/github/github.sha512")
            }
            unsuccessful {
              notifyStatus(
                slackStatus: 'warning',
                subject: "[${env.REPO}] published draft release *${env.TAG_NAME}*: artifacts failed checksum verification.",
                body: "Please verify if the published draft release binaries are healthy."
                      + "\nIf the published draft release binaries are invalid please delete the published draft release."
                      + "\nBuild: (<${env.RUN_DISPLAY_URL}|here>)\nRelease URL: ${env.RELEASE_URL_MESSAGE}"
              )
            }
          }
        }
        stage('Publish release (draft -> regular)') {
          when {
            beforeAgent true
            expression { return env.HAS_APPROVAL_TO_PUBLISH_RELEASE == 'true' }
          }
          options { skipDefaultCheckout() }
          steps {
            withGhEnv(version: '2.4.0') {
              githubReleasePublish(id: "${env.RELEASE_ID}", name: "${env.TAG_NAME}")
            }
          }
          post {
            success {
              notifyStatus(slackStatus: 'good', subject: "[${env.REPO}] Release *${env.TAG_NAME}* has been published", body: "Build: (<${env.RUN_DISPLAY_URL}|here>)\nRelease URL: ${env.RELEASE_URL_MESSAGE}")
            }
          }
        }
      }
      post {
        failure {
          notifyStatus(slackStatus: 'danger', subject: "[${env.REPO}] Release *${env.TAG_NAME}* failed", body: "Build: (<${env.RUN_DISPLAY_URL}|here>)")
        }
        success {
          script {
            currentBuild.description = "${currentBuild.description?.trim() ? currentBuild.description : ''} released"
          }
        }
      }
    }
  }
  post {
    cleanup {
      // Reporting disables in the `internal-ci` since credentials are not in place
      // OTOH it avoids duplicated notifications
      whenFalse(isInternalCI()){
        notifyBuildResult(prComment: true, analyzeFlakey: true, jobName: getFlakyJobName(withBranch: 'main'))
      }
    }
    failure {
      whenTrue(isInternalCI() && env.PRE_RELEASE_STAGE == 'true') {
        notifyStatus(slackStatus: 'danger', subject: "[${env.REPO}] Pre-release steps failed", body: "(<${env.RUN_DISPLAY_URL}|Open>)")
      }
    }
  }
}

/**
* A function to simplify the pipeline and the too large issue in groovy
*/
def packageGeneration(def args = [:]) {
    def local = args.get('local', false)
    args.versions.each { version ->
      unstash "generate-for-package-${version}-Dockerfile"
      unstash "generate-for-package-${version}-Dockerfile.alpine"
    }
    if (local) {
      // VERSION=1.0.0 is needed to override the current version.
      // current version is >1.0.0, and this is the way we can
      // run the agent upgrade testing with some local cached
      // agent distributions, this one and the current version one.
      sh script: "VERSION=1.0.0 make -C packaging package", label: 'package'
      sh script: "mv build/packages build/local", label: 'prepare-local-upgrade-agent'
    } else {
      // Archive the so files to be downloaded if possible.
      archiveArtifacts(allowEmptyArchive: true, artifacts: 'src/ext/modules/*.so')
      sh script: "make -C packaging package", label: 'package'
      sh script: "make -C packaging info", label: 'package info'
      // checksum files are regenerated by the signing component in the internal-ci instance.
      stash(includes: 'build/packages/*', name: 'package', excludes: 'build/packages/**/*.sha512')
    }
}

/**
* Init function to
* Send a GitHub Notification, clean the workspace. restore the source code and run
* the body in the BASE_DIR folder
*/
def initWorkspace(def args = [:], Closure body){
  prepareWorkspace(args) {
    dir("${BASE_DIR}"){
      body()
    }
  }
}

/**
* Package function to
* Send a GitHub Notification, clean the workspace and restore the source, the package code
* and run the body in the BASE_DIR folder
*/
def packageWorkspace(def args = [:], Closure body){
  prepareWorkspace(args) {
    dir("${BASE_DIR}"){
      unstash (args.shouldUseSignedBinaries ? env.SIGNED_ARTIFACTS : 'package')
      // When running in the CI sometimes the docker build might fail for
      // some environmental issues, let's retry
      retryWithSleep(retries: 3, seconds: 5, backoff: true) {
        sh script: "PHP_VERSION=${PHP_VERSION} make -C packaging ${args.prepareGoal}", label: "${args.prepareGoal} for ${PHP_VERSION}"
      }
      body()
    }
  }
}

/**
* Prepare function to
* Send a GitHub Notification, clean the workspace and restore the source code.
*/
def prepareWorkspace(def args = [:], Closure body){
  withGithubNotify(args) {
    deleteDir()
    unstash 'source'
    body()
  }
}

def notifyStatus(def args = [:]) {
  releaseNotification(slackChannel: "${env.SLACK_CHANNEL}",
                      slackColor: args.slackStatus,
                      slackCredentialsId: 'jenkins-slack-integration-token',
                      to: "${env.NOTIFY_TO}",
                      subject: args.subject,
                      body: args.body)
}

def prepareRelease() {
  def tagName = "v${params.VERSION}"
  def branchName = "prepare-release-${tagName}-${env.BUILD_NUMBER}"
  def message = "[${params.VERSION}] Bump version and create changelog"
  def warning = """> THIS PR IS AUTOMATICALLY GENERATED BY RELEASE PIPELINE."""
  def actions = """### Actions
* If changes are fine then:
  1. [Approve](${env.BUILD_URL}input) it. The pipeline will take care of the release.
  1. Close this PR since it is not required anymore.
  1. Delete branch ${branchName}.
* If changes are not correct then:
  1. [abort](${env.BUILD_URL}input) it.
  1. Change and commit changes to this PR.
  1. Merge this PR.
  1. Delete branch ${branchName}.
  1. Run release [build](${env.JOB_URL}) with the VERSION parameter = ${params.VERSION}"""
  def previousVersion = sh(label: 'Get previous version', script: 'make -f .ci/Makefile previous-version', returnStdout: true).trim()
  sh label: 'Bump version', script: "VERSION=${params.VERSION} BRANCH_NAME=${env.BRANCH_NAME} make -f .ci/Makefile bump-version"
  if (sh(label: 'is version bumped?', script: 'git diff-index --quiet HEAD --', returnStatus: true) > 0) {
    // Change ci-tag pointer to be used for generating the changelog
    gitCreateTag(tag: 'ci-tag', tagArgs: '--force', pushArgs: '--force')
    // Create Changelog and verify it contains what's expected.
    sh(label: 'Create changelog', script: "GITHUB_TOKEN=${GITHUB_TOKEN} TAG_NAME=${tagName} PREVIOUS_TAG=v${previousVersion} make -f .ci/Makefile prepare-changelog changelog")
    sh(label: "Git branch ${branchName}", script: """git checkout -b ${branchName}""")
    sh(label: 'Git commit', script: """git commit -a -m "${message}" """)
    def pr = githubCreatePullRequest(title: "[RELEASE] ${params.VERSION}",
                                     description: "${warning}\n\n${actions}",
                                     labels: 'docs,release,changelog',
                                     base: 'main')
    notifyStatus(slackStatus: 'warning', subject: "[${env.REPO}] Prepare ${params.VERSION} release steps to be validated.",
                 body: """Please (<${env.BUILD_URL}input|approve>) it or reject within 12 hours ONLY If no changes are required. Otherwise stop it and review the (<${pr}|PR>).""")
    if (prompt(message: "You are about to release version ${params.VERSION}. If you approve then changes will be committed and pushed. Review ${pr}.")) {
      // Update branch.
      sh(label: "Git branch ${env.BRANCH_NAME}", script: """git checkout ${env.BRANCH_NAME}""")
      sh(label: 'Git rebase', script: """git rebase ${branchName}""")
      gitPush(args: "${env.BRANCH_NAME}")
      gitCreateTag(tag: "${tagName}")
    } else {
      log(level: 'WARN', text: "Please review the PR ${pr}")
    }
  } else {
    gitCreateTag(tag: "${tagName}")
    log(level: 'INFO', text: "There are no changes to compare with. Release will happen from this commit.")
  }
}

def generatePackageLifecycleTestMatrix(def args = [:]) {
  def parallelTasks = [:]
  dir("${BASE_DIR}") {
    def generateScript = ".ci/generate_package_lifecycle_test_matrix.sh"
    def output = sh(label: generateScript, script: generateScript, returnStdout: true)
    if (output?.trim()) {
      output.split('\n').each { testMatrixRow ->
        parallelTasks[testMatrixRow] = generatePackageLifecycleTestMatrixRow(testMatrixRow: testMatrixRow, shouldUseSignedBinaries: args.shouldUseSignedBinaries)
      }
    } else {
      error("${generateScript} returned empty set of data")
    }
  }
  parallel(parallelTasks)
}

def generatePackageLifecycleTestMatrixRow(def args = [:]){
  return {
    withNode(labels: 'ubuntu-18.04 && immutable') {
      def testingArgs =  [:]
      testingArgs['shouldUseSignedBinaries'] = args.shouldUseSignedBinaries
      testingArgs['testMatrixRow'] = args.testMatrixRow
      def parsedTestMatrixRow = args.testMatrixRow.split(',')
      testingArgs['phpVersion'] = parsedTestMatrixRow[0]
      testingArgs['linuxPackageType'] = parsedTestMatrixRow[1]
      def testingType = parsedTestMatrixRow[2]
      if (testingType.equals('lifecycle')) {
        lifecycleTesting(testingArgs)
      }
      if (testingType.equals('lifecycle-apache')) {
        lifecycleTestingOnProdServerKind(testingArgs + [prodServerKind: 'apache'])
      }
      if (testingType.equals('lifecycle-fpm')) {
        lifecycleTestingOnProdServerKind(testingArgs + [prodServerKind: 'fpm'])
      }
      if (testingType.equals('php-upgrade')) {
        phpUpgradeTesting(testingArgs)
      }
      if (testingType.equals('agent-upgrade')) {
        agentUpgradeTesting(testingArgs)
      }
    }
  }
}

def addEnvVarsFromParams(def withEnvList) {
    if (params.AGENT_LOG_LEVEL != null && !params.AGENT_LOG_LEVEL.equals(LOG_LEVEL_NOT_SET)) {
      withEnvList.add('ELASTIC_APM_LOG_LEVEL=' + params.AGENT_LOG_LEVEL)
    }
    if (params.TESTS_LOG_LEVEL != null && !params.TESTS_LOG_LEVEL.equals(LOG_LEVEL_NOT_SET)) {
      withEnvList.add('ELASTIC_APM_PHP_TESTS_LOG_LEVEL=' + params.TESTS_LOG_LEVEL)
    }
    withEnvList.add('ADD_LIBUNWIND_DEPENDENCY=' + params.ADD_LIBUNWIND_DEPENDENCY)
    return withEnvList
}

def runTestingCommand(def args = [:]) {
  try {
    args['context'] = buildWorkspaceContext(args)
    withEnv(addEnvVarsFromParams(["PHP_VERSION=${args.phpVersion}", "ELASTIC_APM_PHP_TESTS_MATRIX_ROW=${args.testMatrixRow}"])) {
      packageWorkspace(args) {
        sh(script: args.testingCommand, label: buildLabel(args))
      }
    }
  } finally {
    junit(allowEmptyResults: true, keepLongStdio: true, testResults: "${BASE_DIR}/build/*junit.xml")
  }
}

def buildWorkspaceContext(def args = [:]) {
  return args.testMatrixRow + (args.shouldUseSignedBinaries ? "-signed-binaries" : "")
}

def buildLabel(def args = [:]) {
  return "Testing for ${args.testMatrixRow}, shouldUseSignedBinaries: ${args.shouldUseSignedBinaries}"
}

def lifecycleTesting(def args = [:]) {
  runTestingCommand(
    args + [
      prepareGoal: "prepare-${args.linuxPackageType}",
      testingCommand: "make -C packaging ${args.linuxPackageType}-lifecycle-testing"
    ]
  )
}

def lifecycleTestingOnProdServerKind(def args = [:]) {
  runTestingCommand(
    args + [
      prepareGoal: "prepare-${args.linuxPackageType}-${args.prodServerKind}",
      testingCommand: "make -C packaging ${args.linuxPackageType}-lifecycle-testing-in-${args.prodServerKind}",
    ]
  )
}

def phpUpgradeTesting(def args = [:]) {
  runTestingCommand(
    args + [
      prepareGoal: "prepare-${args.linuxPackageType}",
      testingCommand: "PHP_VERSION=${args.phpVersion} make -C packaging ${args.linuxPackageType}-php-upgrade-testing"
    ]
  )
}

def agentUpgradeTesting(def args = [:]) {
  withEnv(addEnvVarsFromParams(["PHP_VERSION=${args.phpVersion}", "ELASTIC_APM_PHP_TESTS_MATRIX_ROW=${args.testMatrixRow}"])) {
    initWorkspace(context: buildWorkspaceContext(args + [prefix: 'Agent-upgrade-testing'])) {
      packageGeneration(versions: [args.phpVersion], local: true)
      if (args.shouldUseSignedBinaries) {
          unstash env.SIGNED_ARTIFACTS
      } else {
          unstash 'package'
      }
      sh(script: "make -C packaging ${args.linuxPackageType}-agent-upgrade-testing-local", label: buildLabel(args + [prefix: 'Agent upgrade testing']))
    }
  }
}
